//
// Project:				eXtremeServer Sauerbraten
// File Name: 			eXtreme_ParameterTypes.cfg
// Author: 				BudSpencer
// Date: 				2017
// 
// Purpose: 			define all datatypes for command parser
//



CMDHANDLER_DATATYPES = "i f c w s n"
CMDHANDLER_NAMES = "integer float clientnumber word string name"

CMDHANDLER_INT = "0123456789"
CMDHANDLER_FLOAT = "0123456789."
CMDHANDLER_WORD = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"


eXtreme_isInteger = [
	isint_x = $arg1 ; isint_count = -1 ; isint_res = 1
	cond ( !=s ( format $isint_x ) "" ) [
		loopwhile i ( strlen $isint_x ) [ = $isint_res 1 ] [
			isint_count = ( + $isint_count 1 )
			isint_res = ( ? ( > ( strstr $CMDHANDLER_INT ( substr $isint_x $isint_count 1 ) ) -1 ) 1 0 )
		]
	]
	cond ( =s ( format $isint_x ) "" ) [ isint_res = 0 ]
	result $isint_res
]


eXtreme_isFloat = [
	isfloat_x = $arg1 ; isfloat_count = -1 ; isfloat_res = 1
	cond ( !=s ( format $isfloat_x ) "" ) [
		loopwhile i ( strlen $isfloat_x ) [ = $isfloat_res 1 ] [
			isfloat_count = ( + $isfloat_count 1 )
			isfloat_res = ( ? ( > ( strstr $CMDHANDLER_FLOAT ( substr $isfloat_x $isfloat_count 1 ) ) -1 ) 1 0 )
		]
	]
	cond ( =s ( format $isfloat_x ) "" ) [ isfloat_res = 0 ]
	result $isfloat_res
]


eXtreme_isWord = [
	isword_x = $arg1 ; isword_count = -1 ; isword_res = 1
	isword_currentcharacter = ""
	cond ( !=s ( format $isword_x ) "" ) [
		loopwhile i ( strlen $isword_x ) [ = $isword_res 1 ] [
			isword_count = ( + $isword_count 1 )
			isword_currentcharacter = ( substr $isword_x $isword_count 1 )
			cond ( && ( = ( eXtreme_isInteger $isword_currentcharacter ) 0 ) ( = ( eXtreme_isFloat $isword_currentcharacter ) 0 ) ( = ( strstr $CMDHANDLER_WORD $isword_currentcharacter ) -1 ) ) [
				isword_res = 0
			]
		]
	]
	cond ( =s ( format $isword_x ) "" ) [ isword_res = 0 ]
	cond ( != ( listlen ( format $isword_x ) ) 1 ) [ isword_res = -1 ]
	result $isword_res
]

			


