//
// Project:				eXtremeServer Sauerbraten
// File Name: 			eXtreme_ParameterTypes.cfg
// Author: 				BudSpencer
// Date: 				2017
// 
// Purpose: 			define all datatypes for command parser
//



eXtreme_isInteger = [
	isint_x = $arg1 ; isint_count = -1 ; isint_res = 1
	cond ( !=s ( format $isint_x ) "" ) [
		loopwhile i ( strlen $isint_x ) [ = $isint_res 1 ] [
			isint_count = ( + $isint_count 1 )
			isint_res = ( ? ( > ( strstr $CMDHANDLER_INT ( substr $isint_x $isint_count 1 ) ) -1 ) 1 0 )
		]
	]
	cond ( =s ( format $isint_x ) "" ) [ isint_res = 0 ]
	result $isint_res
]


eXtreme_isFloat = [
	isfloat_x = $arg1 ; isfloat_count = -1
	isfloat_res = 1 ; isfloat_commacount = 0
	cond ( !=s ( format $isfloat_x ) "" ) [
		loop i ( strlen $isfloat_x ) [
			cond ( =s ( substr $isfloat_x $i 1 ) "." ) [
				isfloat_commacount = ( + $isfloat_commacount 1 )
			]
		]
		loopwhile i ( strlen $isfloat_x ) [ = $isfloat_res 1 ] [
			isfloat_count = ( + $isfloat_count 1 )
			isfloat_res = ( ? ( > ( strstr $CMDHANDLER_FLOAT ( substr $isfloat_x $isfloat_count 1 ) ) -1 ) 1 0 )
		]
	]
	cond ( =s ( format $isfloat_x ) "" ) [ isfloat_res = 0 ]
	cond ( != $isfloat_commacount 1 ) [ isfloat_res = 0 ]
	result $isfloat_res
]


eXtreme_isSign = [
	issign_x = ( format $arg1 ) ; issign_count = -1 ; issign_res = 1
	issign_currentcharacter = ""
	cond ( !=s ( format $issign_x ) "" ) [
		loopwhile i ( strlen $issign_x ) [ = $issign_res 1 ] [
			issign_count = ( + $issign_count 1 )
			issign_currentcharacter = ( substr $issign_x $issign_count 1 )
			issign_res = ( ? ( > ( strstr $CMDHANDLER_SIGNS $issign_currentcharacter ) -1 ) 1 0 )
		]
	]
	cond ( =s $issign_x "" ) [ issign_res = 0 ]
	cond ( != ( strlen $issign_x ) 1 ) [ issign_res = 0 ]
	result $issign_res
]


eXtreme_isCharacter = [
	ischar_x = ( format $arg1 ) ; ischar_count = -1 ; ischar_res = 0
	ischar_currentcharacter = ""
	cond ( !=s $ischar_x "" ) [
		loopwhile i ( strlen $CMDHANDLER_OTHERS ) [ = $ischar_res 0 ] [
			ischar_count = $i
			ischar_currentcharacter = ( substr $CMDHANDLER_OTHERS $ischar_count 1 )
			cond ( =s $ischar_x $ischar_currentcharacter ) [
				ischar_res = 1 
			]
		]
	]
	cond ( != ( strlen $ischar_x ) 1 ) [ ischar_res = 0 ]
	result $ischar_res
]


eXtreme_ContainsInvalidCharacters = [
	contains_x = ( format $arg1 )
	contains_res = ""
	contains_invchars = ""
	cond ( !=s $contains_x "" ) [
		cond ( ! ( eXtreme_isInteger $contains_x ) ) [
			cond ( ! ( eXtreme_isFloat $contains_x ) ) [
				loop i ( strlen $contains_x ) [
					cond ( = ( strstr $CMDHANDLER_FLOAT ( substr $contains_x $i 1 ) ) -1 ) [
						cond ( = ( strstr $CMDHANDLER_WORD ( substr $contains_x $i 1 ) ) -1 ) [
							cond ( = ( strstr $CMDHANDLER_SIGNS ( substr $contains_x $i 1 ) ) -1 ) [
								contains_res = "1" 
								cond ( = ( listlen $contains_invchars ) 0 ) [
									contains_invchars = ( concatword $contains_invchars ( escape ( substr $contains_x $i 1 ) ) )
								] [
									contains_invchars = ( concat $contains_invchars ( escape ( substr $contains_x $i 1 ) ) )
								]
							]
						]
					]
				]
			]
		]
	]
	cond ( =s $contains_res "" ) [ contains_res = "0" ]
	cond ( !=s $contains_invchars "" ) [ contains_res = ( concat $contains_res $contains_invchars ) ]
	cond ( =s $contains_x "" ) [ contains_res = 0 ]
	result $contains_res
]			


eXtreme_isWord = [
	isword_x = ( format $arg1 ) ; isword_count = -1 ; isword_res = 1
	isword_currentcharacter = ""
	cond ( !=s ( format $isword_x ) "" ) [
		loopwhile i ( strlen $isword_x ) [ = $isword_res 1 ] [
			isword_count = ( + $isword_count 1 )
			isword_currentcharacter = ( substr $isword_x $isword_count 1 )
			cond ( && ( = ( eXtreme_isInteger $isword_currentcharacter ) 0 ) ( = ( eXtreme_isFloat $isword_currentcharacter ) 0 ) ( = ( strstr $CMDHANDLER_WORD $isword_currentcharacter ) -1 ) ( = ( eXtreme_isSign $isword_currentcharacter ) 0 ) ( = ( eXtreme_isCharacter $isword_currentcharacter ) 0 ) ) [
				isword_res = 0
			]
		]
	]
	cond ( =s ( format $isword_x ) "" ) [ isword_res = 0 ]
	cond ( != ( listlen ( format $isword_x ) ) 1 ) [ isword_res = 0 ]
	result $isword_res
]


eXtreme_isString = [
	isstring_x = ( format $arg1 ) ; isstring_count = -1 ; isstring_res = 1
	isstring_currentword = ""
	cond ( !=s $isstring_x "" ) [
		loopwhile i ( listlen $isstring_x ) [ = $isstring_res 1 ] [
			isstring_count = ( + $isstring_count 1 )
			isstring_currentword = ( at $isstring_x $i )
			cond ( = ( eXtreme_isWord $isstring_currentword ) 0 ) [
				isstring_res = 0
			]
		]
	]
	cond ( =s ( format $isstring_x ) "" ) [ isstring_res = 0 ]
	result $isstring_res
]


