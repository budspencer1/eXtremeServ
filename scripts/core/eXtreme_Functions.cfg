//
// Project:				eXtremeServer Sauerbraten
// File Name: 			eXtreme_Functions.cfg
// Author: 				BudSpencer
// Date: 				2017
// 
// Purpose: 			define useful functions and constants for server
//



EXTREME_ERROR = "[ERROR]" // error prefix
EXTREME_WARNI = "[WARNING]" // warning prefix
EXTREME_INFOS = "[INFORMATION]" // information prefix


COLOR_GREEN = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^f0" ( format $arg1 ) ) ] [ result "^f0" ] ]
COLOR_BLUE = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^f1" ( format $arg1 ) ) ] [ result "^f1" ] ]
COLOR_GOLD = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^f2" ( format $arg1 ) ) ] [ result "^f2" ] ]
COLOR_RED = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^f3" ( format $arg1 ) ) ] [ result "^f3" ] ]
COLOR_GRAY = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^f4" ( format $arg1 ) ) ] [ result "^f4" ] ]
COLOR_MAGENTA = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^f5" ( format $arg1 ) ) ] [ result "^f5" ] ]
COLOR_ORANGE = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^f6" ( format $arg1 ) ) ] [ result "^f6" ] ]
COLOR_WHITE = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^f7" ( format $arg1 ) ) ] [ result "^f7" ] ]
COLOR_RESET = [ cond ( !=s ( format $arg1 ) "" ) [ result ( concatword "^fr" ( format $arg1 ) ) ] [ result "^fr" ] ]


EXTREME_TRUE = 1
EXTREME_FALSE = 0


// some useful constants which can be used for some calculations
CON_PI = 3.14159265358979323846264338327950288419716939937510 // PI 
CON_EN = 2.71828182845904523536028747135266249775724709369995 // euler number
CON_S2 = 1.41421356237309504880168872420969807856967187537694 // squareroot of 2						
CON_S3 = 1.73205080756887729352744634150587236694280525381038 // squareroot of 3																		


// TERMINALOUT
// FORMAT AND OUTPUT TO GAME, CONSOLE OR LOG
eXtreme_Output = [
	output_mode = ""
	output_str = ""
	loop i $numargs [
		cond ( eXtreme_isInteger $[arg@( + $i 1 )] ) [
			output_mode = ( concat $output_mode $[arg@( + $i 1 )] )
		] [
			output_str = ( format $[arg@( + $i 1 )] )
		]
	]
	looplist i $output_mode [
		cases $i "1" [
			say ( format $output_str )
		] "2" [
			echo ( format $output_str )
		]
	]
]


// ERROR
// OUTPUT ERROR MESSAGE TO CONSOLE AND LOG
// DOES NOTHING, IF WE DON'T ENTER AN OUTPUT STRING
eXtreme_Error = [
	error_msg = ( format $arg1 )
	cond ( !=s $error_msg "" ) [
		eXtreme_Output 2 ( concat $EXTREME_ERROR $error_msg )
	]
]


// WARNING
// OUTPUT WARNING MESSAGE TO CONSOLE AND LOG
// DOES NOTHING, IF WE DON'T ENTER AN OUTPUT STRING
eXtreme_Warning = [
	warn_msg = ( format $arg1 )
	cond ( !=s $warn_msg "" ) [
		eXtreme_Output 2 ( concat $EXTREME_WARNI $warn_msg )
	]
]


// INFORMATION
// OUTPUT INFO MESSAGE TO CONSOLE AND LOG
// DOES NOTHING, IF WE DON'T ENTER AN OUTPUT STRING
eXtreme_Information = [
	info_msg = ( format $arg1 )
	cond ( !=s $info_msg "" ) [
		eXtreme_Output 2 ( concat $EXTREME_INFOS $info_msg )
	]
]







