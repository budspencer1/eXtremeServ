//
// Project:				eXtremeServer Sauerbraten
// File Name: 			eXtreme_EvLibrary.cfg
// Author: 				BudSpencer
// Date: 				2017
// 
// Purpose: 			define useful functions for event handler etc.
//



eXtreme_EvHandlerExists = [
	hanexists_handler = ( format $arg1 )
	hanexists_res = 0
	cond ( eXtreme_CheckForArguments $hanexists_handler ) [
		looplist cur_handler $EVENTLIST_IDENTS [
			cond ( ! $hanexists_res ) [
				hanexists_res = ( ? ( =s $cur_handler $hanexists_handler ) 1 0 )
			]
		]
	]
	result $hanexists_res
]


eXtreme_GetEventType = [
	getevent_ident = ( format $arg1 )
	getevent_res = "-1"
	getevent_cur = ""
	cond ( eXtreme_CheckForArguments $getevent_ident ) [
		getevent_res = ""
		loop cur_ident ( listlen $EVENTLIST_IDENTS ) [
			getevent_cur = ( at $EVENTLIST_IDENTS $cur_ident )
			cond ( =s $getevent_cur $getevent_ident ) [
				getevent_res = ( at $EVENTLIST_ETYPES $cur_ident )
			]
		]
	]
	result ( format $getevent_res )
]


eXtreme_GetIdentList = [
	getlist_evtype = ( format $arg1 )
	getlist_res = "-1"
	cond ( eXtreme_CheckForArguments $getlist_evtype ) [
		cond ( > ( indexof $EVENTHANDLER_EVNAMES $getlist_evtype ) -1 ) [
			getlist_res = ""
			loop cur_evtype ( listlen $EVENTLIST_ETYPES ) [
				cond ( =s ( at $EVENTLIST_ETYPES $cur_evtype ) $getlist_evtype ) [
					cond ( ! ( listlen $getlist_res ) ) [
						getlist_res = ( concatword $getlist_res ( at $EVENTLIST_IDENTS $cur_evtype ) )
					] [
						getlist_res = ( concat $getlist_res ( at $EVENTLIST_IDENTS $cur_evtype ) )
					]
				]
			]
		] [
			EVERR_INVALIDTYPE $getlist_evtype
		]
	]
	cond ( =s $getlist_res "" ) [ getlist_res = "-1" ] // put -1 if nothing has been found
	result $getlist_res 
]


eXtreme_EventExists = [
	evexist_event = ( format $arg1 ) ; evexist_res = 0
	cond ( eXtreme_CheckForArguments $evexist_event ) [
		looplist cur_ev $EVENTHANDLER_EVNAMES [
			cond ( ! $evexist_res ) [
				cond ( =s $cur_ev $evexist_event ) [
					evexist_res = 1 
				]
			]
		]
	]
	result $evexist_res
]